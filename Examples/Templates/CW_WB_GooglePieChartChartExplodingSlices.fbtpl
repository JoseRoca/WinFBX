2
FBGUI
.bas
CWindow: WebBrowser: Google Pie Chart - Exploding slices
' ########################################################################################
' Microsoft Windows
' File: CW_WB_GooglePieChartChartExplodingSlices.fbtpl
' Contents: WebBrowser - Google pie chart - Exploding slices
' Compiler: FreeBasic 32 & 64 bit
' Copyright (c) 2017 José Roca. Freeware. Use at your own risk.
' THIS CODE AND INFORMATION IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EITHER
' EXPRESSED OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE IMPLIED WARRANTIES OF
' MERCHANTABILITY AND/OR FITNESS FOR A PARTICULAR PURPOSE.
' ########################################################################################

#define UNICODE
#INCLUDE ONCE "Afx/CWindow.inc"
#INCLUDE ONCE "Afx/CWebBrowser/CWebBrowser.inc"
USING Afx

CONST IDC_WEBBROWSER = 1001

DECLARE FUNCTION WinMain (BYVAL hInstance AS HINSTANCE, _
                          BYVAL hPrevInstance AS HINSTANCE, _
                          BYVAL szCmdLine AS ZSTRING PTR, _
                          BYVAL nCmdShow AS LONG) AS LONG

   END WinMain(GetModuleHandleW(NULL), NULL, COMMAND(), SW_NORMAL)

' // Forward declaration
DECLARE FUNCTION WndProc (BYVAL hwnd AS HWND, BYVAL uMsg AS UINT, BYVAL wParam AS WPARAM, BYVAL lParam AS LPARAM) AS LRESULT

' ========================================================================================
' HTML script to build a Google chart
' ========================================================================================
PRIVATE FUNCTION BuildPieChart () AS STRING

   DIM s AS STRING

   s  = "<!DOCTYPE html>"
   s += "<html>"
   s += "<head>"
   s += "   <meta http-equiv='X-UA-Compatible' content='IE=edge' />"
   s += "   <meta http-equiv='MSThemeCompatible' content='Yes'>"
   s += "    <script type='text/javascript' src='https://www.gstatic.com/charts/loader.js'></script>"
   s += "    <script type='text/javascript'>"
   s += "      google.charts.load('current', {packages:['corechart']});"
   s += "      google.charts.setOnLoadCallback(drawChart);"
   s += "      function drawChart() {"
   s += "        var data = google.visualization.arrayToDataTable(["
   s += "          ['Language', 'Speakers (in millions)'],"
   s += "          ['Assamese', 13], ['Bengali', 83], ['Bodo', 1.4],"
   s += "          ['Dogri', 2.3], ['Gujarati', 46], ['Hindi', 300],"
   s += "          ['Kannada', 38], ['Kashmiri', 5.5], ['Konkani', 5],"
   s += "          ['Maithili', 20], ['Malayalam', 33], ['Manipuri', 1.5],"
   s += "          ['Marathi', 72], ['Nepali', 2.9], ['Oriya', 33],"
   s += "          ['Punjabi', 29], ['Sanskrit', 0.01], ['Santhali', 6.5],"
   s += "          ['Sindhi', 2.5], ['Tamil', 61], ['Telugu', 74], ['Urdu', 52]"
   s += "        ]);"
   s += "        var options = {"
   s += "          title: 'Indian Language Use',"
   s += "          legend: 'none',"
   s += "          pieSliceText: 'label',"
   s += "          slices: {  4: {offset: 0.2},"
   s += "                    12: {offset: 0.3},"
   s += "                    14: {offset: 0.4},"
   s += "                    15: {offset: 0.5},"
   s += "          },"
   s += "        };"
   s += "        var chart = new google.visualization.PieChart(document.getElementById('piechart'));"
   s += "        chart.draw(data, options);"
   s += "      }"
   s += "    </script>"
   s += "  </head>"
   s += "  <body>"
   s += "    <div id='piechart' style='width: 600px; height: 400px;'></div>"
   s += "  </body>"
   s += "</html>"

   FUNCTION = s

END FUNCTION
' ========================================================================================

' ========================================================================================
' Main
' ========================================================================================
FUNCTION WinMain (BYVAL hInstance AS HINSTANCE, _
                  BYVAL hPrevInstance AS HINSTANCE, _
                  BYVAL szCmdLine AS ZSTRING PTR, _
                  BYVAL nCmdShow AS LONG) AS LONG

   ' // Set process DPI aware
   ' // The recommended way is to use a manifest file
   AfxSetProcessDPIAware

   ' // Creates the main window
   DIM pWindow AS CWindow
   ' -or- DIM pWindow AS CWindow = "MyClassName" (use the name that you wish)
   DIM hwndMain AS HWND = pWindow.Create(NULL, "WebBrowser - Google Pie Chart - Exploding slices", @WndProc)
   ' // Sizes it by setting the wanted width and height of its client area
   pWindow.SetClientSize(600, 400)
   ' // Centers the window
   pWindow.Center

   ' // Add a WebBrowser control
   DIM pwb AS CWebBrowser = CWebBrowser(@pWindow, IDC_WEBBROWSER, 0, 0, pWindow.ClientWidth, pWindow.ClientHeight)
   ' // Set the IDocHostUIHandler interface
   pwb.SetUIHandler

   ' // Build the script
   DIM s AS STRING = BuildPieChart
   ' // Save the script as a temporary file
   DIM wszPath AS WSTRING * MAX_PATH = AfxSaveTempFile(s, "html")
   ' // Navigate to the path
   pwb.Navigate(wszPath)
   ' // Processes pending Windows messages to allow the page to load
   ' // Needed if the message pump isn't running
   AfxPumpMessages
   ' // Kill the temporary file
   KILL wszPath

   ' // Displays the window and dispatches the Windows messages
   FUNCTION = pWindow.DoEvents(nCmdShow)

END FUNCTION
' ========================================================================================

' ========================================================================================
' Main window procedure
' ========================================================================================
FUNCTION WndProc (BYVAL hwnd AS HWND, BYVAL uMsg AS UINT, BYVAL wParam AS WPARAM, BYVAL lParam AS LPARAM) AS LRESULT

   SELECT CASE uMsg

      CASE WM_COMMAND
         SELECT CASE GET_WM_COMMAND_ID(wParam, lParam)
            CASE IDCANCEL
               ' // If ESC key pressed, close the application by sending an WM_CLOSE message
               IF GET_WM_COMMAND_CMD(wParam, lParam) = BN_CLICKED THEN
                  SendMessageW hwnd, WM_CLOSE, 0, 0
                  EXIT FUNCTION
               END IF
         END SELECT

      CASE WM_SIZE
         ' // Optional resizing code
         IF wParam <> SIZE_MINIMIZED THEN
            ' // Retrieve a pointer to the CWindow class
            DIM pWindow AS CWindow PTR = AfxCWindowPtr(hwnd)
            ' // Move the position of the control
            IF pWindow THEN pWindow->MoveWindow GetDlgItem(hwnd, IDC_WEBBROWSER), _
               0, 0, pWindow->ClientWidth, pWindow->ClientHeight, CTRUE
         END IF

    	CASE WM_DESTROY
         ' // Ends the application by sending a WM_QUIT message
         PostQuitMessage(0)
         EXIT FUNCTION

   END SELECT

   ' // Default processing of Windows messages
   FUNCTION = DefWindowProcW(hwnd, uMsg, wParam, lParam)

END FUNCTION
' ========================================================================================
